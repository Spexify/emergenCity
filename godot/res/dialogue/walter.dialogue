~ START
Walter: Guten Tag!
Avatar: Guten Tag.
~ BRANCH
- Avatar: Was macht die Arbeit?
	=> TALK
- Avatar: Was hast du so im Angebot?
	=>< TRADE
	=> BRANCH
- Avatar: Tschüss!
	Walter: Bis dann!
	=> END


~ TALK
Walter: Momentan handel ich ohne Geld. Wer weiß wie viel es morgen wert ist.
Avatar: Nur [i]Ware[/i] ist Wahres?
Walter: [#mood=happy]Hahaha. So könnte man es ausdrücken.
=> END


~ TRADE
do TradeMngr.setup_next_trade("Walter")
Walter: Für {{TradeMngr.get_sought_items_text()}} biete ich dir {{TradeMngr.get_offered_items_text()}}.
if TradeMngr.is_trade_feasible()
	Walter: Was meinst du?
	- Avatar: Geht klar.
		do TradeMngr.execute_trade()
		Walter: [#mood=happy]Danke.
	- Avatar: Grade nicht.
		Walter: Okay, schönen Tag noch!
else
	Walter: Tut mir Leid, aber {{TradeMngr.get_unfeasibility_reason()}}!
=> END