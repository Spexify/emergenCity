~ START
Mert: Hey Kumpel! Alles klar bei dir?
- Avatar: Ja, wie läuft's bei dir?
	%2 => DIALOG1
	%2 => DIALOG2
- Avatar: Ich muss los!
	Mert: Man sieht sich!
	=> END

~ DIALOG1
Mert: Mir gehts super! Kommst du gut durch die Krise?
- Ja
	Avatar: Ich komme klar.
	Mert: Schön zu hören! Aber pass' trotzdem auf dich auf, ja? So eine Krise ist nicht ohne.
- Nein
	Avatar: Ist ehrlich gesagt ganz schön schwierig.
	Mert: Tut mir leid, das zu hören. Wenn du jemandem zum Reden brauchst, sag Bescheid, ja?
	Mert: Zusammen schaffen wir das schon!
=> END

~ DIALOG2
Mert: Bin ein bisschen gestresst. In solchen Zeiten hat das THW, also das Technische Hilfswerk, mehr zu tun als sonst.
Mert: Aber zu sehen, wie viel Hilfsbereitschaft und Dankbarkeit die Menschen haben - Das ist das Größte.
=> END


~ THW_AIDKITS
if not has_executed_consequences_of_event("THW_AIDKITS")
	=> FREE_ITEMS
else
	Sorry, aber du hast bereits dein Paket erhalten.
=> END


~ FREE_ITEMS
Mert: Wir vom Technischen Hilfswerk verteilen momentan kostenlose Hilfspakete um die Krise zu bekämpfen!
Mert: Hier ist dein Paket.[wait=0.5] [b]Weiterhin viel Erfolg![b]
do execute_consequences("THW_AIDKITS")
=> END