~ START
Raphael: Guten Tag!
Avatar: Guten Tag.
~ BRANCH
- Avatar: Was macht die Arbeit?
	=> TALK
- Avatar: Was hast du so im Angebot?
	=>< TRADE
	=> BRANCH
- Avatar: Tschüss!
	Raphael: Bis dann!
	=> END


~ TALK
Raphael: Momentan handel ich ohne Geld. Wer weiß wie viel es morgen wert ist.
Avatar: Nur [i]Ware[/i] ist Wahres?
Raphael: [#mood=happy]Hahaha. So könnte man es ausdrücken.
=> END


~ TRADE
do TradeMngr.setup_next_trade("Raphael")
Raphael: Für {{TradeMngr.get_sought_items_text()}} biete ich dir {{TradeMngr.get_offered_items_text()}}.
if TradeMngr.is_trade_feasible()
	Raphael: Was meinst du?
	- Avatar: Geht klar.
		do TradeMngr.execute_trade()
		Raphael: [#mood=happy]Danke.
	- Avatar: Grade nicht.
		Raphael: Okay, schönen Tag noch!
else
	Raphael: Tut mir Leid, aber {{TradeMngr.get_unfeasibility_reason()}}!
=> END


~ NEIGHBORHOOD_PARTY
if not has_executed_consequences_of_event("NEIGHBORHOOD_PARTY")
	=> FREE_FOOD
else
	Raphael: Hast du schon mit den anderen geschnackt?
=> END


~ FREE_FOOD
Raphael: Willkommen auf der Nachbarschaftsfete!
Raphael: Wir mussten ja jede Menge Krisen in letzter Zeit durchstehen.
Raphael: Endlich hat sich der Bürgermeister durchgerungen, das Fest hier zu finanzieren.
Raphael: Was willst du haben?
- Bratfisch mit Pommes, bitte
	Raphael: Einmal Bratfisch und Pommes, verstanden!
	do execute_extra_consequence("add_items_by_name", "FISH_N_CHIPS")
	do flag_consequences_as_executed("NEIGHBORHOOD_PARTY")
- Einen Teller Gulasch, bitte
	Raphael: Ein Teller Gulasch kommt sofort!
	do execute_extra_consequence("add_items_by_name", "GOULASH")
	do flag_consequences_as_executed("NEIGHBORHOOD_PARTY")
- Reis mit Gemüse, bitte
	Raphael: Hier!
	do execute_extra_consequence("add_items_by_name", "RICE_WITH_VEGETABLES")
	do flag_consequences_as_executed("NEIGHBORHOOD_PARTY")
Avatar: Danke!
=> END