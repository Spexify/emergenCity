~ START
Agathe: Oh toll, endlich wieder Besuch!
~ BRANCH
Agathe: Bist du zum Tauschen hier?
- Nein, lass uns quatschen!
	%2 => DIALOG1
	%2 => DIALOG2
	%2 => DIALOG3
- Ja, ich will handeln.
	=>< TRADE
	=> BRANCH
- Nichts dergleichen.
	=> END

~ DIALOG1
Agathe: Wie reizend! Seitdem diese Krise angefangen hat, traue ich mich kaum raus.
Agathe: Einkaufen kann man auch nichts mehr! Gut, dass mich meine Nichte ab und zu besucht.
Agathe: Und deine Besuche sind auch immer sehr schön.
Avatar: Finde ich auch!
=> END

~ DIALOG2
Agathe: Wie schön! Kennst du schon meinen Nachbarn Mert? Ein sehr netter Mensch.
Agathe: Seine Katze Momo ist sehr niedlich, aber leider auch sehr scheu.
Agathe: Ob ich mir auch ein Haustier zulegen sollte? Es wäre schön, Gesellschaft zu haben.
Agathe: Aber es ist auch viel Verantwortung, besonders in Krisenzeiten.
=> END

~ DIALOG3
Agathe: Wie schön! Kennst du schon meine Nachbarn Kris und Veronika? Ein sehr liebes Pärchen.
Agathe: Veronika ist sehr organisiert und akribisch, aber wusstest du, dass von den beiden Kris sich um die Pflanzen kümmert?
Agathe: Kris wirkt zwar oft etwas verträumt, aber die Pflanzen sind Kris wichtig, das merkt man.
Agathe: Als die beiden das letzte Mal im Camping-Urlaub waren, hat Kris mir genau erklärt, welche Pflanze wie oft gegossen werden muss.
Agathe: Und da alle Pflanzen gut überlebt haben, darf ich sie beim nächsten Urlaub bestimmt wieder gießen.
=> END

~ TRADE
do TradeMngr.setup_next_trade("Agathe")
Avatar: Was hast du zum Tauschen?
Agathe: Ich will {{TradeMngr.get_sought_items_text()}} und biete dir dafür {{TradeMngr.get_offered_items_text()}}.
if TradeMngr.is_trade_feasible()
	Agathe: Einverstanden?
	- Avatar: Okay!
		do TradeMngr.execute_trade()
		Agathe: [#mood=happy]Entzückend!
	- Avatar: Nein Danke.
		Agathe: [#mood=sad][i]Schade[/i]..
else
	Agathe: [#mood=sad]Oh jemine, aber {{TradeMngr.get_unfeasibility_reason()}}!
=> END


~ NEIGHBORHOOD_PARTY
Agathe: Beinahe hätte ich diese Fete hier verpasst!
Agathe: Gut, dass ich immer Radio höre.
=> END